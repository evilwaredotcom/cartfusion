<cfsilent>
<cfparam name="attributes.forcedFlag" default="false">

<cfif not isDefined("attributes.executionMode") and thisTag.executionMode eq "start">
  <cfset attributes.executionMode = thisTag.executionMode>
  <cfset attributes.forcedFlag = true>
</cfif>

<cfif attributes.forcedFlag and thisTag.executionMode eq "end">
  <cfset attributes.executionMode = "end">
</cfif>
</cfsilent>
<cfswitch expression="#attributes.executionMode#">
<cfcase value="start">
  <cfsilent>
	<cfif not thisTag.hasEndTag and not isDefined("attributes.skipEnd")>
	  <cfthrow message="jContainer requires an end tag.">
	</cfif>

	<!--- Skin --->
	<cfparam name="attributes.skin" default="default">
	<cfif structKeyExists(request, "jComponent") AND structKeyExists(request.jComponent, "skins") AND structKeyExists (request.jComponent.skins, attributes.skin)>
	  <cfloop collection="#request.jComponent.skins[attributes.skin]#" item="i">
  	  <cfif not structKeyExists(attributes, i)>
  		<cfset attributes[i] = request.jComponent.skins[attributes.skin][i]>
  	  </cfif>
	  </cfloop>
	</cfif>

	<cfparam name="attributes.label" default="jContainer">
	<cfparam name="attributes.border" default="1">
	<cfparam name="attributes.width" default="100%">
	<cfparam name="attributes.height" default="0">
	<cfparam name="attributes.borderColor" default="666666">
	<cfparam name="attributes.borderHighlightColor" default="FFFFFF">
	<cfparam name="attributes.borderLowlightColor" default="CCCCCC">
	<cfparam name="attributes.bgColor" default="FFFFFF">
	<cfparam name="attributes.fgColor" default="000000">
	<cfparam name="attributes.titleBgColor" default="EAEAEA">
	<cfparam name="attributes.titleFgColor" default="000000">
	<cfparam name="attributes.rollOverBgColor" default="ffffff">
	<cfparam name="attributes.rollOverFgColor" default="0066FF">
	<cfparam name="attributes.multiple" default="true">
	<cfparam name="attributes.underline" default="false">
	<cfparam name="attributes.font" default="Verdana">
	<cfparam name="attributes.fontSize" default="12">
	<cfparam name="attributes.underline" default="false">
	<cfparam name="attributes.bold" default="true">
	<cfparam name="attributes.open" default="true">
	<cfparam name="attributes.indent" default="10">
	<cfparam name="attributes.closeable" default="true">
	<cfparam name="attributes.height" default="0">
	<cfparam name="attributes.borderHighlight" default="1">
	<cfparam name="attributes.icon" default="">
	<cfparam name="attributes.iconClosed" default="">
	<cfparam name="attributes.name" default="#createUuid()#">
	<cfparam name="attributes.onChange" default="">

	<cfset child = "j" & replaceNoCase(createUuid(), "-", "", "all")>
  </cfsilent>
  <cfset msg="<!-- Control generated by jComponents 1.0, copyright #datePart("yyyy", now())# joe rinehart, joe.rinehart@gmail.com, http://clearsoftware.net/jComponents -->">
  <cfoutput>#msg#</cfoutput>
  <script>
	<cfif not isDefined("request.jComponent.init")>
	  <cfset request.jComponent.init = true>
	  jComponents = new Object;
	  jComponents.go = function() {};
	  jComponents.getComponent = function(id) {
		return jComponents[id];
	  }

	  jComponents.setBarColor = function (bgIdArray, fgIdArray, bgColor, fgColor) {
		var i=0
		for (i=0;i<bgIdArray.length;i++) try { (document.getElementById(bgIdArray[i]).style.background = bgColor) } catch(e) {};
		for (i=0;i<fgIdArray.length;i++) try { (document.getElementById(fgIdArray[i]).style.color = fgColor) } catch(e) {};
	  }
	</cfif>

	<cfif not isDefined("request.jComponent.container")>
	  <cfset request.jComponent.container = true>
	  jComponents.setContainer = function(id, isOpen) {
		var cont = document.getElementById(jComponents[id].child);
		jComponents[id].isOpen = isOpen;
		if (isOpen) {
		  cont.style.display = '';
		  if (jComponents[id].icon.length) document.getElementById("i" + jComponents[id].child).src = jComponents[id].icon;
		  jComponents[id].isOpen = true;
		} else {
		  cont.style.display = 'none';
		  if (jComponents[id].iconClosed.length) document.getElementById("i" + jComponents[id].child).src = jComponents[id].iconClosed;
		  jComponents[id].isOpen = false;
		}
	  }
	</cfif>

	<cfoutput>
	  jComponents["#attributes.name#"] = {name:"#attributes.name#",isOpen:<cfif attributes.open>true<cfelse>false</cfif>,child:"#child#",icon:"#attributes.icon#",iconClosed:"#attributes.iconClosed#"}
	</cfoutput>
  </script>
  <cfoutput>
  <table width="#attributes.width#" cellpadding="0" cellspacing="0" border="0"
	style="border:#attributes.border#px ###attributes.borderColor# Solid;
		   cursor:hand;"
	onMouseOver="jComponents.setBarColor(['b#child#','b1#child#','b2#child#'], ['t#child#'], '###attributes.rollOverBgColor#', '###attributes.rollOverFgColor#')"
	onMouseOut="jComponents.setBarColor(['b#child#','b1#child#','b2#child#'], ['t#child#'], '###attributes.titleBgColor#', '###attributes.titleFgColor#')"
	onClick="Javascript:<cfif attributes.closeable>var result = jComponents.setContainer('#attributes.name#', (document.getElementById('#child#').style.display == 'none') ? true : false);<cfif len(attributes.onChange)>var result = #attributes.onChange#()</cfif><cfelse>1=1</cfif>"
  >
	<tr>
	  <td id="b#child#" style="background:###attributes.titleBgColor#;<cfif attributes.borderHighlight>border-top:#attributes.borderHighlight#px ###attributes.borderHighlightColor# Solid;border-left:#attributes.borderHighlight#px ###attributes.borderHighlightColor# Solid;border-bottom:#attributes.borderHighlight#px ###attributes.borderLowlightColor# Solid;</cfif>">
		 <table cellpadding="0" cellspacing="0" border="0">
	  <tr>
		<cfif len(attributes.icon) or len(attributes.iconClosed)>
		<td id="b1#child#" style="background:###attributes.titleBgColor#;padding-left:2px;padding-right:4px;padding-top:2px;font-family:#attributes.font#;font-size:#attributes.fontSize#px;cursor:hand;" valign="top">
		  <img id="i#child#" src="<cfif (attributes.open and len(attributes.icon)) or (not attributes.open and not len(attributes.iconClosed))>#attributes.icon#<cfelse>#attributes.iconClosed#</cfif>" border="0"></span>
		</td>
		</cfif>
		<td valign="top" id="b2#child#" style="background:###attributes.titleBgColor#;">
		  <a href="Javascript:jComponents.go()"
			 id = "t#child#"
			 style="color:###attributes.titleFgColor#;
					font-family:#attributes.font#;
					font-size:#attributes.fontSize#px;
					cursor:hand;
					<cfif attributes.bold>font-weight:bold;</cfif>
					<cfif not attributes.underline>text-decoration:none;</cfif>"
			 >
			  <div style="width:100%;padding:2px;">#attributes.label#</div>
		  </a>
		</td>
	  </tr>
	  </table>
	  </td>
	</tr>
  </table>
  <table width="#attributes.width#" height="#attributes.height#" cellpadding="0" cellspacing="0" border="0" id="#child#" style="
	border-left:#attributes.border#px ###attributes.borderColor# Solid;
	border-right:#attributes.border#px ###attributes.borderColor# Solid;
	border-bottom:#attributes.border#px ###attributes.borderColor# Solid;
	display:none;
  ">
  <tr>
	<td valign="top" style="background:###attributes.bgColor#;">
  </cfoutput>
</cfcase>
<cfcase value="end">
	</td>
  </tr>
  </table>
  <cfif attributes.open>
	<cfoutput>
	<script>
	  jComponents.setContainer("#attributes.name#", "true")
	</script>
	</cfoutput>
  </cfif>
</cfcase>
</cfswitch>