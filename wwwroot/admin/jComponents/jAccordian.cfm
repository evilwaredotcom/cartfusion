<cfswitch expression="#thisTag.executionMode#">
<cfcase value="start">
  <cfsilent>
	<cfif not thisTag.hasEndTag>
	  <cfthrow message="jAccordian requires an end tag.">
	</cfif>
	
	<!--- Skin --->
	<cfparam name="attributes.skin" default="default">
	<cfif structKeyExists(request, "jComponent") AND structKeyExists(request.jComponent, "skins") AND structKeyExists (request.jComponent.skins, attributes.skin)>
	  <cfloop collection="#request.jComponent.skins[attributes.skin]#" item="i">
  	  <cfif not structKeyExists(attributes, i)>
  		<cfset attributes[i] = request.jComponent.skins[attributes.skin][i]>
  	  </cfif>
	  </cfloop>
	</cfif>
	
	<!--- Attribs --->
	<cfparam name="attributes.width" default="100%">
	<cfparam name="attributes.height" default="0">
	<cfparam name="attributes.name" default="j#replaceNoCase(createUuid(), "-", "", "all")#">
	<cfparam name="attributes.border" default="1">
	<cfparam name="attributes.borderColor" default="666666">
	<cfparam name="attributes.bgColor" default="FFFFFF">
	<cfparam name="attributes.fgColor" default="000000">
	<cfparam name="attributes.multiple" default="false">
	<cfparam name="attributes.font" default="Verdana">
	<cfparam name="attributes.onChange" default="">

	<cfif attributes.height contains "%">
	  <cfthrow message="jComponent error: jAccordian does not support percentage-based heights.">
	</cfif>
		
	<cfif len(attributes.name) and reFindNoCase("[^0-9A-Za-z]", attributes.name) or isNumeric(left(attributes.name, 1))>
	  <cfthrow message="jComponent error:  The ID attribute may only contain letters and numbers, and may not start with a number.">
	</cfif>	
	<cfset paneNames = arrayNew(1)>
	<cfset activeFlag = false>
	<cfset openIndexes = "">
  </cfsilent>
  <cfoutput>
  <cfset msg="<!-- Control generated by jComponents 1.0, copyright #datePart("yyyy", now())# joe rinehart, joe.rinehart@gmail.com, http://clearsoftware.net/jComponents -->">
  <cfoutput>#msg#</cfoutput>
  <script>
	<cfif not isDefined("request.jComponent.init")>
	  <cfset request.jComponent.init = true>
	  jComponents = new Object;
	  jComponents.go = function() {};
	  jComponents.getComponent = function(id) {
		return jComponents[id];
	  }
	  
	  jComponents.setBarColor = function (bgIdArray, fgIdArray, bgColor, fgColor) {
		var i=0
		for (i=0;i<bgIdArray.length;i++) try { (document.getElementById(bgIdArray[i]).style.background = bgColor) } catch(e) {};
		for (i=0;i<fgIdArray.length;i++) try { (document.getElementById(fgIdArray[i]).style.color = fgColor) } catch(e) {};
	  }
	</cfif>

	<cfif not isDefined("request.jComponent.accodian")>
	  <cfset request.jComponent.accordian = true>
	  jComponents.setAccordian = function(id, index) {
		var activeName = jComponents[id].activePane = jComponents[id].panes[index];
		
		for (var i=0;i<jComponents[id].panes.length;i++) {
		  if (jComponents[id].panes[i] == activeName) jComponents[id].selectedIndex = i;
  
		  if (!jComponents[id].multiple) {
			document.getElementById(jComponents[id].panes[i]).style.display = 'none';
			try { document.getElementById("i" + jComponents[id].panes[i]).style.display = 'none' } catch(e) {};
			try { document.getElementById("ic" + jComponents[id].panes[i]).style.display = '' } catch(e) {};
		  }
		}
  
		var state = document.getElementById(activeName).style.display;
		if (state == '') {
		  document.getElementById(activeName).style.display = 'none';
		  try { document.getElementById("i" + activeName).style.display = 'none'} catch(e) {};
		  try { document.getElementById("ic" + activeName).style.display = ''} catch(e) {};
		} else {
		  try { document.getElementById("i" + activeName).style.display = ''} catch(e) {};
		  try { document.getElementById("ic" + activeName).style.display = 'none'} catch(e) {};
		  document.getElementById(activeName).style.display = '';
		}
  
		jComponents[id].onChange();
	  }
	</cfif>
	
	jComponents["#attributes.name#"] = new Object;
	jComponents["#attributes.name#"] = {panes:new Array,defaultPanes:new Array,name:"#attributes.name#",activePane:"",selectedIndex:0,multiple:<cfif attributes.multiple>true<cfelse>false</cfif>,onChange:function() {<cfif len(attributes.onChange)>#attributes.onChange#();</cfif>return}}
  </script>
  <table cellpadding="0" cellspacing="0" border="0" style=""><!--- background:###attributes.bgColor#;border-top:#attributes.border#px ###attributes.borderColor# Solid;border-left:#attributes.border#px ###attributes.borderColor# Solid;border-right:#attributes.border#px ###attributes.borderColor# Solid;width:#attributes.width# --->
  <tr>
	<td valign="top">
  </cfoutput>
</cfcase>
<cfcase value="end">
	</td>
	</tr>
  </table>
  <script>
	<cfoutput>
	<cfif not listLen(openIndexes)>
	  jComponents.setAccordian("#attributes.name#", 0);
	<cfelse>
	  <cfloop list="#openIndexes#" index="i">
		jComponents.setAccordian("#attributes.name#", #i#);
	  </cfloop>
	</cfif>
	</cfoutput>
  </script>
</cfcase>
</cfswitch>
